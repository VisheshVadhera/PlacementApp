apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        applicationId "com.vishesh.tpc_stud"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        resValue "string", "base_url", BASE_URL
        resValue "string", "facebook_app_id", FACEBOOK_APP_ID
        resValue "string", "account_kit_client_token", ACCOUNT_KIT_CLIENT_TOKEN
        manifestPlaceholders = [
                facebook_app_id         : FACEBOOK_APP_ID,
                account_kit_client_token: ACCOUNT_KIT_CLIENT_TOKEN,
                ak_login_protocol_scheme: "ak${FACEBOOK_APP_ID}"
        ]
        testInstrumentationRunner 'com.vishesh.tpc_stud.core.MockTestRunner'
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res/splash',
                    'src/main/res/dashboard',
                    'src/main/res/core',
                    'src/main/res/auth',
                    'src/main/res'
            ]
        }
    }
}

ext {

    //App dependencies
    supportLibVersion = '24.0.0'
    okHttpVersion = '3.6.0'
    daggerVersion = '2.8'
    rxVersion = '2.0.1'
    retrofitVersion = '2.1.0'
    butterknifeVersion = '8.5.1'
    accountKitVersion = '4.+'
    jodaTimeVersion = '2.9.5.1'
    permissionsDispatcherVersion = '2.3.1'
    androidJobQueueVersion = '1.1.7'

    //Test dependencies
    junitVersion = '4.12'
    mockitoVersion = '1.10.19'
    powerMockito = '1.6.2'
    hamcrestVersion = '1.3'
    runnerVersion = '0.5'
    rulesVersion = '0.5'
    espressoVersion = '2.2.2'
}

repositories {
    jcenter()
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    //Support Library
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"

    //Butterknife
    compile "com.jakewharton:butterknife:${butterknifeVersion}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterknifeVersion}"

    //Accountkit
    compile "com.facebook.android:account-kit-sdk:${accountKitVersion}"

    //Dagger 2
    compile "com.google.dagger:dagger:${daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"

    //Retrofit 2
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    //RxJava
    compile "io.reactivex.rxjava2:rxandroid:${rxVersion}"
    compile "io.reactivex.rxjava2:rxjava:${rxVersion}"

    //Okhttp
    compile "com.squareup.okhttp3:okhttp:${okHttpVersion}"
    compile "com.squareup.okhttp3:logging-interceptor:${okHttpVersion}"

    //LoaderView
    compile 'com.github.ybq:Android-SpinKit:1.1.0'

    //RxBindings for views
    compile 'com.github.JakeWharton.RxBinding:rxbinding:d7774aae78'
    compile 'com.github.JakeWharton.RxBinding:rxbinding-design:d7774aae78'

    //Optionals library
    compile 'com.fernandocejas:arrow:1.0.0'

    //JodaTime
    compile "net.danlew:android.joda:${jodaTimeVersion}"

    //AdapterDelegates for heterogeneous recycler views
    compile 'com.hannesdorfmann:adapterdelegates3:3.0.1'

    //Circle ImageView
    compile 'de.hdodenhof:circleimageview:2.1.0'

    //Iconics for material design icons
    compile "com.mikepenz:iconics-core:2.8.2@aar"
    compile 'com.mikepenz:material-design-iconic-typeface:2.2.0.2@aar'

    //Retrofit mock
    compile "com.squareup.retrofit2:retrofit-mock:${retrofitVersion}"

    //For loading images
    compile 'com.squareup.picasso:picasso:2.5.2'

    //RxRelays for Event
    compile 'com.jakewharton.rxrelay2:rxrelay:2.0.0'

    //Annotations handling permissions for Android M+ devices
    compile "com.github.hotchemi:permissionsdispatcher:${permissionsDispatcherVersion}"
    annotationProcessor "com.github.hotchemi:permissionsdispatcher-processor:${permissionsDispatcherVersion}"

    //Persistent Job Queue
    compile "com.evernote:android-job:${androidJobQueueVersion}"

    compile "com.android.support.test.espresso:espresso-idling-resource:${espressoVersion}"

    //For executing unit tests locally
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.mockito:mockito-all:${mockitoVersion}"
    testCompile "org.hamcrest:hamcrest-all:${hamcrestVersion}"
    testCompile "org.powermock:powermock-module-junit4:${powerMockito}"
    testCompile "org.powermock:powermock-api-mockito:${powerMockito}"

    // Android Testing Support Library's runner and rules
    androidTestCompile("com.android.support.test:runner:${runnerVersion}") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test:rules:${rulesVersion}") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    // Espresso UI Testing dependencies.
    androidTestCompile("com.android.support.test.espresso:espresso-core:${espressoVersion}") {
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:${espressoVersion}") {
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestCompile("com.android.support.test.espresso:espresso-intents:${espressoVersion}") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestCompile('com.squareup.assertj:assertj-android:1.1.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestCompile ("com.jakewharton.espresso:okhttp3-idling-resource:1.0.0"){
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
        exclude group: 'com.squareup.okio', module: 'okio'
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"

}
